apply plugin: 'java'
apply plugin: 'groovy'

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options']
}

compileTestJava {
    options.encoding = defaultEncoding
}

compileGroovy {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options']
    groovyOptions.encoding = 'utf-8'
}

compileTestGroovy {
    options.encoding = defaultEncoding
}

configurations {
    testOutput.extendsFrom( testCompile )
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.codehaus.groovy:groovy-all:2.1.8'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.springframework:spring-test:4.1.1.RELEASE'
    testCompile 'org.slf4j:slf4j-simple:1.7.10'
    testOutput sourceSets.test.output
}

task testJar( type: Jar ) {
    group 'build'
    description 'Creates a jar file with all test sources.'
    from sourceSets.test.output
    classifier = 'test'
}

task sourceJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    testOutput testJar
    archives sourceJar
}

sourceCompatibility = '1.8'
targetCompatibility = sourceCompatibility

test {
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'
}

