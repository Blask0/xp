apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

compileTestJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

configurations {
    testOutput.extendsFrom testRuntime
}

ext {
    felixVersion = '6.0.1'
}

dependencies {
    testCompile 'junit:junit:4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.1.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.springframework:spring-test:4.1.6.RELEASE'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
    testOutput sourceSets.test.output
}

task testJar( type: Jar ) {
    group 'build'
    description 'Creates a jar file with all test sources.'
    from sourceSets.test.output
    classifier = 'test'
}

task sourcesJar( type: Jar ) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = sourceCompatibility

test {
    useJUnitPlatform()
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    manifest {
        attributes( ['Implementation-Version': project.version] )
    }
}

publishing.publications {
    mavenJava( MavenPublication ) {
        artifact sourcesJar
    }
}
