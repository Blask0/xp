apply plugin: 'java-base'

configurations {
    lib
    bundle
    bundle.transitive = false
}

dependencies {
    lib project( ':launcher' )
}

ext {
    installDir = "$buildDir/install"
    bundlesDir = "$installDir/system"
    bundlesFile = "$projectDir/src/system/bundles.xml"
    archiveBase = "enonic-xp-$version"
}

def getProjectMappings()
{
    def map = [:]
    project.rootProject.subprojects.each {
        map["${it.group}:${it.name}:${it.version}"] = it
    }
    return map
}

def expandGav( gav )
{
    def template = new groovy.text.SimpleTemplateEngine()
    return template.createTemplate( gav ).make( project.properties ).toString();
}

def addBundleDependencies()
{
    def mappings = getProjectMappings()
    def bundlesXml = new XmlSlurper().parse( bundlesFile )

    bundlesXml.bundle.each {
        def gav = expandGav( it.text().trim() )
        def dep = mappings[gav]

        if ( !dep )
        {
            dep = gav
        }

        dependencies.bundle dep
    }
}

addBundleDependencies();

task copyBundles( type: Copy ) {
    into bundlesDir
    from configurations.bundle
    rename { name ->
        def artifacts = configurations.bundle.resolvedConfiguration.resolvedArtifacts
        def artifact = artifacts.find { it.file.name == name }
        def id = artifact.moduleVersion.id
        def dir = id.group.replace( '.', '/' ) + "/${id.name}/${id.version}"
        def fileName = "${id.name}-${id.version}"

        if ( artifact.classifier != null )
        {
            fileName += '-' + artifact.classifier
        }

        return "${dir}/${fileName}.${artifact.extension}"
    }
}

task copyResources( type: Copy ) {
    destinationDir = file( installDir )
    into( 'bin' ) {
        from( 'src/bin' ) {
            include '*.sh'
            fileMode 0755
        }
    }
    into( 'tools' ) {
        from( 'src/tools' ) {
            include '*.sh'
            fileMode 0755
        }
    }
    into( 'lib' ) {
        from( configurations.lib )
    }
    into( 'system' ) {
        from( bundlesFile ) {
            expand project.properties
        }
    }
    from( 'src' ) {
        include 'README.txt'
        expand project.properties
    }
    from( 'src' )
    from( rootDir ) {
        include 'LICENSE.txt'
        include 'NOTICE.txt'
    }
}

task installDist( dependsOn: [copyResources, copyBundles] )

task distTar( type: Tar, dependsOn: installDist ) {
    compression = Compression.GZIP
    extension = 'tar.gz'
    from installDir
    into archiveBase
}

task distZip( type: Zip, dependsOn: installDist ) {
    from installDir
    into archiveBase
}

artifacts {
    archives distTar
    archives distZip
}
