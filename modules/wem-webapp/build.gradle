apply plugin: 'war'

dependencies {
    compile project( ':wem-web' )
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
}

publishing {
    publications {
        maven {
            from components.web
        }
    }
}

task npmInstall( type: Exec ) {
    group = 'Grunt'
    description = 'Install Node dependencies.'
    executable = winOrUnixExec( 'npm.cmd', 'npm' )
    args = ['install']
    inputs.file 'package.json'
    outputs.dir 'node_modules'
}

task gruntAll( type: Exec, dependsOn: npmInstall ) {
    group = 'Grunt'
    description = 'Execute all grunt tasks.'
    executable = findGruntExec()
    args = ['all']
    inputs.file 'Gruntfile.js'
    inputs.dir 'src/main/webapp'
    outputs.dir 'src/main/webapp'
}

war.dependsOn = ['gruntAll']

def isWindows( )
{
    return System.getProperty( 'os.name' ).toLowerCase().contains( 'windows' )
}

def winOrUnixExec( winName, unixName )
{
    return isWindows() ? winName : unixName;
}

def findGruntExec( )
{
    String gruntExec = winOrUnixExec( 'grunt.cmd', 'grunt' )
    File localGrunt = project.file( "node_modules/grunt-cli/bin/${gruntExec}" )
    if ( localGrunt.isFile() )
    {
        gruntExec = localGrunt.toString()
    }

    return gruntExec
}
