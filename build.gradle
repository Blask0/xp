apply plugin: 'java-base'
apply from: "$rootDir/gradle/check.gradle"
apply from: "$rootDir/gradle/sonar.gradle"

allprojects {
    group = 'com.enonic.xp'

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.com/public'
        }
    }

    buildDir = 'target'
}

ext {
    javaProjects = subprojects - project( ':distro' )
    itestProjects = subprojects.findAll { it.name.endsWith( '-itest' ) }
    osgiProjects = javaProjects - project( ':launcher' ) - itestProjects
    mavenProjects = subprojects
}

configure( javaProjects ) {
    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/checkstyle.gradle"
}

configure( osgiProjects ) {
    apply from: "$rootDir/gradle/osgi.gradle"
}

configure( mavenProjects ) {
    apply from: "$rootDir/gradle/maven.gradle"
}

